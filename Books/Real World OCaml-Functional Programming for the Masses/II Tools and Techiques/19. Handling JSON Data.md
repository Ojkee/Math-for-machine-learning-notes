### `Yojson.Basic.t` and JSON Representation
#### JSON Type Definition
- Defined as:
```ocaml
type json = [
	| `Assoc of (string * json) list
	| `Bool of bool
	| `Float of float
	| `Int of int
	| `List of json list
	| `Null
	| `String of string
]
```
- Represents standard JSON constructs:
    - `Assoc`: key-value pairs (objects)
    - `Bool`, `Float`, `Int`, `Null`, `String`, `List`

### Reading JSON
#### From String
- Function:
```ocaml
val from_string : string -> json
```
Reads JSON data from a string.

#### From File
- Function:
```ocaml
val from_file : string -> json
```
Reads JSON data directly from a file.

#### From Channel
- Function:
```ocaml
val from_channel : in_channel -> json
```

#### Example: Parsing a JSON File
```ocaml
open Core

let () =
	let buf = In_channel.read_all "book.json" in
	let json1 = Yojson.Basic.from_string buf in
	let json2 = Yojson.Basic.from_file "book.json" in
	print_endline (if Yojson.Basic.equal json1 json2 then "OK" else "FAIL")
```

### JSON Field Access and Extraction
#### Utility Functions (`Yojson.Basic.Util`)
```ocaml
member : string -> json -> json 

to_string : json -> string

to_int : json -> int

filter_string : json list -> string list

to_bool_option : json -> bool option
```

#### Example
```ocaml
open Core

let () =
	let json = Yojson.Basic.from_file "book.json" in
	let open Yojson.Basic.Util in
	let title = json |> member "title" |> to_string in
	let tags = json |> member "tags" |> to_list |> filter_string in
	let pages = json |> member "pages" |> to_int in
	let is_online = json |> member "is_online" |> to_bool_option in
	let is_translated = json |> member "is_translated" |> to_bool_option in

	let authors = json |> member "authors" |> to_list in
	let names =
		List.map authors ~f:(fun json -> member "name" json |> to_string) 
	in

	printf "Title: %s (%d)\n" title pages;
	printf "Authors: %s\n" (String.concat ~sep:", " names);
	printf "Tags: %s\n" (String.concat ~sep:", " tags);
	
	let string_of_bool_option = function
		| None -> "<unknown>"
		| Some true -> "yes"
		| Some false -> "no"
	in
	printf "Online: %s\n" (string_of_bool_option is_online);
	printf "Translated: %s\n" (string_of_bool_option is_translated)
```

#### Sample JSON Input (`book.json`)
```json
{
  "title": "Real World OCaml",
  "tags": ["functional programming", "ocaml", "algorithms"],
  "pages": 450,
  "is_online": true,
  "authors": [
    { "name": "Jason Hickey" },
    { "name": "Anil Madhavapeddy" },
    { "name": "Yaron Minsky" }
  ]
}
```
- Output:
```bash
Title: Real World OCaml (450)
Authors: Jason Hickey, Anil Madhavapeddy, Yaron Minsky
Tags: functional programming, ocaml, algorithms
Online: yes
Translated: <unknown>
```

### Constructing JSON Values
```ocaml
let person = `Assoc [("name", `String "Anil")];;
Yojson.Basic.pretty_to_string person;;
```

### Serialization and Printing
- Pretty Print to Channel:
```ocaml
Yojson.Basic.pretty_to_channel stdout person;;
```

### Safe JSON (`Yojson.Safe`)
#### Extra Constructors
```ocaml
type json = [
	| `Assoc of (string * json) list
	| `Bool of bool
	| `Float of float
	| `Floatlit of string
	| `Int of int
	| `Intlit of string
	| `List of json list
	| `Null
	| `String of string
	| `Stringlit of string
	| `Tuple of json list
	| `Variant of string * json option
]
```
```ocaml
to_basic : json -> Yojson.Basic.t
```
- Converts complex JSON (with `variants`, `tuples`, `lit values`) to basic form.
#### Example conversions:
```ocaml
Variant ("A", None) → String "A"
Variant ("B", Some x) → List [String "B", x]
Intlit "12345678901234567890" → String "12345678901234567890"
```

### Tooling
#### `opam` Installation
```bash
opam install yojson
```

Build with Dune
```dune
(executable
 (name parse_book)
 (libraries core yojson))
```

```bash
dune build parse_book.exe
dune exec -- ./parse_book.exe
```

### `atdgen`: Automatic Code Generation for JSON
- Installation and Version Check
```bash
opam install atdgen
atdgen -version
```
    
#### `ATD` (Abstract Type Description)
- Used to describe structured data types.
- Used by `atdgen` to generate:
    - OCaml type definitions (`-t`)
    - JSON serializers/deserializers (`-j`)

### Example: GitHub Authorization JSON
- `ATD` Definition (`github.atd`)

```ocaml
type scope = [
	| User         <json name="user">
	| Public_repo  <json name="public_repo">
	| Repo         <json name="repo">
	| Repo_status  <json name="repo_status">
	| Delete_repo  <json name="delete_repo">
	| Gist         <json name="gist">
]
```
```ocaml
type app = {
	name: string;
	url: string;
} <ocaml field_prefix="app_">
```
```ocaml
type authorization_request = {
	scopes: scope list;
	note: string;
} <ocaml field_prefix="auth_req_">
```
```ocaml
type authorization_response = {
	scopes: scope list;
	token: string;
	app: app;
	url: string;
	id: int;
	?note: string option;
	?note_url: string option;
}
```

### Generated Files
#### Code Generation
```bash
atdgen -t github.atd   # generates github_t.ml and github_t.mli
atdgen -j github.atd   # generates github_j.ml and github_j.mli
```

#### Example Output: `github_t.mli`
```ocaml
type scope =
	[ `Delete_repo 
	| `Gist 
	| `Public_repo 
	| `Repo 
	| `Repo_status 
	| `User ]
```
```ocaml
type app = {
	app_name : string;
	app_url : string;
}
```
```ocaml
type authorization_request = {
	auth_req_scopes : scope list;
	auth_req_note : string;
}
```
```ocaml
type authorization_response = {
	scopes : scope list;
	token : string;
	app : app;
	url : string;
	id : int;
	note : string option;
	note_url : string option;
}
```

### Serialization (in `github_j.mli`)
#### Example Functions
- Serialize to JSON string:
```ocaml
val string_of_authorization_request :
	?len:int -> authorization_request -> string
```
```ocaml
val string_of_authorization_response :
	?len:int -> authorization_response -> string
```
- Default buffer size is 1024 unless specified with `?len`

### Summary of `atdgen` Usage
#### Pipeline Overview
- Write `.atd` file defining types
- Run:
```bash
atdgen -t file.atd
atdgen -j file.atd
```
- Use generated modules:
    - `Github_t` for types
    - `Github_j` for JSON conversion
#### Example OCaml Usage
```ocaml
let json = Github_j.string_of_authorization_response response
let parsed = Github_j.authorization_response_of_string json
```

### Example: GitHub Organization
#### Example JSON
```json
{
	"login": "janestreet",
	"id": 3384712,
	"url": "https://api.github.com/orgs/janestreet",
	"name": null,
	"blog": null,
	"email": null,
	"public_repos": 145
}
```
#### `ATD` Definition (`github_org.atd`)
```ocaml
type org = {
	login: string;
	id: int;
	url: string;
	?name: string option;
	?blog: string option;
	?email: string option;
	public_repos: int;
}
```

#### Code Generation for Organization
- Generate Types
```bash
atdgen -t github_org.atd
```
- Produces: `github_org_t.ml`, `github_org_t.mli`

#### Output Example: `github_org_t.mli`
```ocaml
type org = {
	login: string;
	id: int;
	url: string;
	name: string option;
	blog: string option;
	email: string option;
	public_repos: int;
}
```

#### Generate JSON Serialization
```bash
atdgen -j github_org.atd
```
- Produces: `github_org_j.ml`, `github_org_j.mli`

#### Output Example: `github_org_j.mli`
```ocaml
type org = Github_org_t.org = { ... }

val write_org : Bi_outbuf.t -> org -> unit
val string_of_org : ?len:int -> org -> string
val read_org : Yojson.Safe.lexer_state -> Lexing.lexbuf -> org
val org_of_string : string -> org
```

### OCaml Program to Fetch GitHub Organization
- Required Package
```bash
opam install shell
```
- OCaml Code (`github_org_info.ml`)
```ocaml
open Core

let print_org file () =
	let url = sprintf "https://api.github.com/orgs/%s" file in
	Shell.run_full "curl" [url]
		|> Github_org_j.org_of_string
		|> fun org ->
			let open Github_org_t in
			let name = Option.value ~default:"???" org.name in
			printf "%s (%d) with %d public repos\n"
			name org.id org.public_repos

let () =
	Command.basic_spec ~summary:"Print Github organization information"
	Command.Spec.(empty +> anon ("organization" %: string))
	print_org |> Command.run
```

#### Example Usage
```bash
dune exec -- ./github_org_info.exe mirage
```
```bash
MirageOS (131943) with 125 public repos
```

```bash
dune exec -- ./github_org_info.exe janestreet
```
```bash
??? (3384712) with 145 public repos
```

### Dune Configuration for `atdgen`
- Rule to Generate JSON Code
```dune
(rule
	(targets github_org_j.ml github_org_j.mli)
	(deps github_org.atd)
	(mode fallback)
	(action (run atdgen -j %{deps})))
```

- Rule to Generate Type Code
```dune
(rule
	(targets github_org_t.ml github_org_t.mli)
	(deps github_org.atd)
	(mode fallback)
	(action (run atdgen -t %{deps})))
```

- Executable Declaration
```dune
(executable
	(name github_org_info)
	(libraries core yojson atdgen shell)
	(flags :standard -w -32)
	(modules github_org_info github_org_t github_org_j))
```
